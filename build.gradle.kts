plugins {
    java
    alias(libs.plugins.runvelocity)
    alias(libs.plugins.idea.ext)
    alias(libs.plugins.blossom)
    alias(libs.plugins.shadow)
}

repositories {
    maven("https://repo.papermc.io/repository/maven-public/")
}

dependencies {
    compileOnly(libs.velocity)
    annotationProcessor(libs.velocity)
    compileOnly(libs.authmevelocity)
    implementation(libs.hikaricp) {
        isTransitive = false
    }
    implementation(libs.h2database)
}

tasks {
    build {
        dependsOn(shadowJar)
    }
    compileJava {
        options.encoding = Charsets.UTF_8.name()
        options.release.set(21)
    }
    clean {
        // Deletes the directory that is generated by the runVelocity plugin
        delete("run")
    }
    runVelocity {
        velocityVersion(libs.versions.velocity.get())
    }
    shadowJar {
        archiveBaseName.set(rootProject.name)
        archiveClassifier.set("")
        relocate("com.zaxxer.hikari", "io.github._4drian3d.authmevelocity.lastserver.libs.hikari")
        relocate("org.h2", "io.github._4drian3d.authmevelocity.lastserver.libs.h2")
    }
}

sourceSets {
    main {
        blossom {
            javaSources {
                property("version", project.version.toString())
            }
        }
    }
}

java.toolchain.languageVersion.set(JavaLanguageVersion.of(21))
