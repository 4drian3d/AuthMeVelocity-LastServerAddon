plugins {
    java
    alias(libs.plugins.runvelocity)
    alias(libs.plugins.blossom)
    alias(libs.plugins.shadow)
}

repositories {
    maven("https://repo.papermc.io/repository/maven-public/")
}

dependencies {
    compileOnly(libs.velocity)
    annotationProcessor(libs.velocity)
    compileOnly(libs.authmevelocity)
    implementation(libs.hikaricp) {
        isTransitive = false
    }
    implementation(libs.h2database)
    implementation(libs.configurate.hocon)
}

tasks {
    build {
        dependsOn(shadowJar)
    }
    compileJava {
        options.encoding = Charsets.UTF_8.name()
        options.release.set(17)
    }
    clean {
        // Deletes the directory that is generated by the runVelocity plugin
        delete("run")
    }
    runVelocity {
        velocityVersion(libs.versions.velocity.get())
    }
    shadowJar {
        archiveBaseName.set(rootProject.name)
        archiveClassifier.set("")
        relocate("com.zaxxer.hikari", "io.github._4drian3d.authmevelocity.lastserver.libs.hikari")
        relocate("org.h2", "io.github._4drian3d.authmevelocity.lastserver.libs.h2")
        relocate("org.spongepowered", "io.github._4drian3d.authmevelocity.lastserver.libs.sponge")
        relocate("io.leangen.geantyref", "io.github._4drian3d.authmevelocity.lastserver.libs.geantyref")
    }
}

blossom {
    replaceTokenIn("src/main/java/io/github/_4drian3d/authmevelocity/lastserver/Constants.java")
    replaceToken("{version}", project.version)
}

java.toolchain.languageVersion.set(JavaLanguageVersion.of(17))
